import "pkg:/source/ListMixin.bs"
import "pkg:/source/RowItemScroller.bs"
import "pkg:/source/RowItemViewManager.bs"
import "pkg:/source/ItemFocusManager.bs"
import "pkg:/source/BaseRow.bs"

namespace ml
  @node("ml_HorizontalRow", "ml_BaseRow")
  class HorizontalRow extends ml.BaseRow

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ additional private state
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    public focusedCellIndex = 0
    public focusedCellPercent = 0.0
    public isScrolling = false

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ managers and private state
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private viewManager
    private focusManager
    private itemScroller
    private currentHeaderType = invalid
    private settings

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ views
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private backgroundGroup
    private cellsGroup
    private contentGroup
    private foregroundGroup
    private header

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ view tracking
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private componentPool = m.global.componentPool

    function new()
      super()
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ initialization
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    protected override function createViews()
      m.backgroundGroup = m.createSGNode("Group", m.top, "backgroundGroup")
      m.contentGroup = m.createSGNode("Group", m.top, "contentGroup")
      m.cellsGroup = m.createSGNode("Group", m.contentGroup, "cellsGroup")
      m.foregroundGroup = m.createSGNode("Group", m.top, "foregroundGroup")
      m.createManagers()
    end function

    private function createManagers()
      m.log.info("createManagers")
      m.viewManager = new ml.RowItemViewManager(m.owner, m.top, m.cellsGroup, m.componentPool, 0, m.width)
      m.itemScroller = new ml.RowItemScroller(m.viewManager)
      m.focusManager = new ml.ItemFocusManager(m.viewManager, m.itemScroller, m.owner.cellEvents, m.index)
      m.focusManager.name = "ROW FM "
      m.focusManager.isRow = true
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ overridden
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    protected override function onContentChange(content as mc.types.node, keepOldFocusIfPossible = false as boolean)
      m.log.info("onContentChange")
      m.configureHeader()
      m.calculateHeight()
      if content <> invalid
        m.viewManager.setContent(content)

        m.focusManager.isNotifying = keepOldFocusIfPossible = invalid or keepOldFocusIfPossible = true
        m.itemScroller.reset(keepOldFocusIfPossible)
        m.focusManager.isNotifying = true
      end if
    end function

    protected override function onIsRenderedChange()
      if m.viewManager <> invalid
        m.viewManager.updateRenderedComponents(m.itemScroller.direction, m.itemScroller.index)
      end if
    end function

    protected override function onListEvent(event as object)
      if event.name = "willEnter" or event.name = "didEnter"
        for each component in m.cellsGroup.getChildren(-1, 0)
          if component.doesExist("listEvent")
            component.listEvent = event
          end if
        end for
      end if
    end function

    protected override function onAnimateToItemChange(index as integer)
      if m._currentContent <> invalid
        m.itemScroller.moveToIndex(index)
      end if
    end function

    public override function getRenderer(index as integer)
      return m.viewManager.getRenderer(index)
    end function

    public override function getRect(index as integer, useScreenCoords = false as boolean)
      return m.viewManager.getRect(index, useScreenCoords)
    end function

    public override function getScreenRect(index as integer, screenPos = invalid as integer, direction = 0 as integer)
      rect = m.viewManager.getScreenRect(index, screenPos, direction)
      if rect <> invalid
        rect.translation[1] += m.contentGroup.translation[1]
      end if
      return rect
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ header
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private function configureHeader()
      if m._currentContent = invalid
        m.cleanHeader()
      else
        headerSettings = m.settings.headerSettings
        if m.currentHeaderType = invalid or m.currentHeaderType <> headerSettings.compName
          m.cleanHeader()
          if headerSettings.position = "onTop"
            m.header = m.createSGNode(headerSettings.compName, m.foregroundGroup, "header")
          else if headerSettings.position = "underneath"
            m.header = m.createSGNode(headerSettings.compName, m.backgroundGroup, "header")
          end if
        end if

        if m.header <> invalid
          m.header.translation = [- m.settings.contentOffset[0], 0] 'cancel out this row's offset
          m.header.width = m.width
          m.header.height = headerSettings.height
          m.header@.setContent(m._currentContent, headerSettings)
          m.contentGroup.translation = [0, headerSettings.height]
        else
          m.contentGroup.translation = [0, 0]
        end if
      end if
    end function

    private function cleanHeader()
      if m.currentHeaderType <> invalid
        m.currentHeaderType = invalid
        m.header.getParent().removeChild(m.header)
        m.header = invalid
      end if
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ public api
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ private impl
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    protected override function calculateHeight() as integer
      if m.settings = invalid
        return 0
      end if

      if m.settings.height <> -1
        height = m.settings.height
      else
        height = m.settings.cellSettings.size[1]
      end if

      if m.header <> invalid
        height += m.settings.headerSettings.height
      end if
      return height
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ KeyPress
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private function onKeyPressLeft() as boolean
      m.log.verbose("onKeyPressLeft")
      m.itemScroller.move(-1)
      return true
    end function

    private function onKeyPressRight() as boolean
      m.log.verbose("onKeyPressRight")
      m.itemScroller.move(1)
      return true
    end function

    private function getLongPressIntervalForKey(key as string) as float
      m.log.verbose("getLongPressIntervalForKey", key)
      if key = "left" or key = "right"
        return 0.2
      else
        return 0
      end if
    end function

    private function onLongPressStart(key as string) as boolean
      m.log.verbose(">>>>onLongPressStart", key)

      if key = "left" or key = "right"
        m.log.verbose("long press finished on key", key, "going to animate to final scroll position")
        if key = "left"
          m.itemScroller.moveToEnd(-1)
        else
          m.itemScroller.moveToEnd(1)
        end if

        return true
      end if

      return false
    end function

    private function onLongPressFinish(key as string) as boolean
      if key = "left" or key = "right"
        m.log.verbose("long press finished on key", key, "going to animate to final scroll position")
        m.itemScroller.finishAnimating()
        return true
      end if

      return false
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ delegate
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    protected function onWillLoseFocus(direction as integer)
      m.log.verbose("This row will lose focus - cancelling scrolling")
      m.itemScroller.cancelScrolling()
      m.toggleLongPressTimer(0)
    end function

  end class
end namespace